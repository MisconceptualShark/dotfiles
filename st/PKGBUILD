pkgname=st
pkgver=0.8.1
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url='http://st.suckless.org/'
_patches=('https://st.suckless.org/patches/anysize/st-anysize-20180817.diff'
          'https://st.suckless.org/patches/scrollback/st-scrollback-0.8.diff'
          'https://st.suckless.org/patches/vertcenter/st-vertcenter-20180320-6ac8c8a.diff'
#          'https://st.suckless.org/patches/xresources/st-xresources-20180309-c5ba9c0.diff'
          'https://st.suckless.org/patches/externalpipe/st-externalpipe-0.8.1.diff')
source=(http://dl.suckless.org/st/$pkgname-$pkgver.tar.gz
        config.h
        ${_patches[@]})
md5sums=('92135aecdba29300bb2e274a55f5b71e'
         'bda636a23139193ada2705272300aa48'
         '9effc30b79e07c3dc9d2a1e41b9dd6d1'
         'bbe056eaed5914f55ccea001ca7f05e9'
         '51106ec8ff04d64029401421bbc57ab5'
         '6c1d71ee8f8abfc3976536a17a376d20'
         '7d23416418e9930e4cba177244c2c085')


prepare() {
  cd $srcdir/$pkgname-$pkgver
  # skip terminfo which conflicts with nsurses
  sed -i '/tic /d' Makefile
  # cut out config.def.h patch for scrollback (manually added to config.h)
  sed -i '1,13d' "$srcdir/$(basename ${_patches[1]})"

  for patch in "${_patches[@]}"; do
    patch -Np1 -i "$srcdir/$(basename $patch)"
  done

  cp $srcdir/config.h config.h
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
